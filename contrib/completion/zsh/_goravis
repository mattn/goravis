#compdef goravis

typeset -A opt_args

_goravis() {
  local -a commands __repo __help
  commands=(
  'help:Show help'
  'accounts:displays accounts and their subscription status'
  'branches:displays the most recent build for each branch'
  'disable:disable a project'
  'enable:enables a project'
  'encrypt:encrypts values for the .travis.yml'
  'env:env'
  'login:authenticates against the API and stores the token'
  'logout:deletes the stored API token'
  'logs:streams test logs'
  'open:opens a build or job in the browser'
  "pubkey:prints out a repository's public key"
  'repos:lists repositories the user has certain permissions on'
  'status:checks status of the latest build'
  'sync:triggers a new sync with GitHub'
  'token:outputs the secret API token'
  'version:outputs the client version'
  'whatsup:lists most recent builds'
  'whoami:displays accounts and their subscription status'
  )

  __repo=(
  {-r,--repo}'[repository]:Repository:'
  )

  __help=(
  '(- *)--help[Show context-sensitive help (also try --help-long and --help-man)]'
  )

  _arguments \
    ${__help[@]} \
    "1: :{_describe 'goravis command' commands}" \
    '*:: :->args'

  case $state in
    args) # for future use

    case $words[1] in
    (encrypt)
      _arguments \
        ${__repo[@]} \
        ${__help[@]} \
        '*:data to encrypt:'
      ;;

    (env)
      local -a env_cmds
      env_cmds=(
      'list:list env'
      'set:set env'
      )

      _arguments \
        ${__repo[@]} \
        ${__help[@]} \
        "1: :{_describe 'env command' env_cmds}" \
        '*:: :->args'

      case $state in
        args)
          case $words[1] in
            list)
              _arguments \
              ${__repo[@]} \
              ${__help[@]}
              ;;

            set)
              _arguments \
                ${__repo[@]} \
                ${__help[@]} \
                '*:env to set:'
              ;;
          esac
          ;;
      esac
      ;;

    (branches|disable|enable|logs|open|pubkey|repos|status|sync)
      _arguments \
        ${__repo[@]} \
        ${__help[@]} \
      ;;

    (accounts|login|logout|token|version|whatsup|whoami)
      _arguments \
        ${__help[@]}
        ;;
    esac
    ;;
  esac
}

_goravis

# vim:ft=zsh
